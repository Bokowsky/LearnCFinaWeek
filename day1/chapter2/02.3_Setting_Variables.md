# 2.3 Settings Variables

ColdFusion makes storing information very easy because it's a loosely typed Language. You can stick any kind of data into a ColdFusion variable without having to tell ColdFusion what kind of data it is.

### Here's the simplest code to set a variable:

```cfml
<cfset thisIs = "fun">

In CFScript:

thisIs = "fun";
```

You can look at the contents of a variable in ColdFusion by using the cfdump tag.

```cfml
<cfdump var = "#thisIs#">

In CFScript:

writeDump(thisIs);
```

You'll note in CFTag we surrounded the variable name with '#' (pound or hash symbol). The pound sign tells ColdFusion to evaluate the contents. In this example, the pound sign will tell ColdFusion to replace the 'thisIs' string with the contents of the previously defined variable ``thisIs``.

Here's another way to look at pound sign use. Examine the below three statements. Can you guess what will happen in each case?

```cfml
<cfdump var = "1 + 2"><br>
<cfdump var = "#1 + 2#"><br>
<cfdump var = "1 + 2 IS #1 + 2#"><br>

In CFScript:

writeDump("1 + 2");
writeDump("#1 + 2#");
writeDump("1 + 2 IS #1 + 2#");
```

In the first statement, our math problem is surrounded by the Double Quote character. This tells ColdFusion to treat the statement as a string and do no evaluations on it.

In the second statement, we also surrounded the math problem with the pound sign. ColdFusion executes the statement and performs our calculation for us.

In the third statement, notice we've mixed strings and evaluations through the correct use of the pound sign and the double quotes.

## The Left Side Of The Statement: '\<cfset thisIs'

The left side of the statement is the variable name. You can have numbers as part of the variable name, but the variable name must start with a letter or an underscore. Spaces and most special characters are not allowed in variable names. A good rule to follow is variable names should be descriptive and help provide context to what is being done.

For example, the word **variable** is a bad naming choice because the name adds no context to why the variable exists:

```cfml
variable = "12/26/1975";
```

The variable name **userBirthdate** is a good naming choice because it adds context to why the variable exists:

```cfml
userBirthdate = "12/26/1975";
```

> **Pro Tip:** You'll understand the most about WHY your program is written a certain way as you are writing it. Take advantage of that hard-earned understanding and leave yourself (or others) as many clues and as much context as possible. Later on, after you've forgotten some of the details, it'll be much easier to piece together why the program was written a certain way and it'll be quicker to make good updates to your program.

## The Right Side Of The Statement: '"fun"\>'

The right side of the statement contains the value for the variable. Simple strings are enclosed in single or double quotes, with double quotes being the most common.

The right side of the statement is an execution zone. This means ColdFusion will attempt to evaluate items on the right side of the statement.

## Examples

Here are a few examples that will help you understand setting ColdFusion variables:

### Print Out the Current Date

```cfml
<cfset dateToday = now()>
<cfdump var = "#dateToday#">

In CFScript:

dateToday = now();
writeDump(dateToday);
```

If you were to run the code example you would see how the ColdFusion function now(), which gets the servers current date and time, was evaluated and the contents placed in the variable DateToday? Anything not specifically in quotes (double or single) will be evaluated. It is possible to evaluate items in quotes, if you use the '#' sign. Keep this in mind as you develop ColdFusion. Novices have a tendency to overuse the pound sign.

### Alternate Method: Print Out the Current Date

The code below will evaluate to the same thing as in the previous step.

```cfml
<cfset dateToday = "#now()#">
<cfdump var = "#dateToday#">

In CFScript:

dateToday = "#now()#";
writeDump("#dateToday#");
```

However, unless there are strings in the quotes, it's generally preferred to use the previous method.

To mix execution and strings, do this:

### Mix Execution and Strings

```cfml
<cfset dateToday = "Today is: #now()#">

In CFScript:

dateToday = "Today is: #now()#";
```

### Concatenation

You could also concatenate the strings using the '&' operator. This example is functionally equal to the previous example:

```cfml
<cfset DateToday = "Today is: " & now()>

In CFScript:

dateToday = "Today is: " & now();
```

So is this one:

```cfml
<cfset dateToday = "Today is: ">
<cfset dateToday = dateToday & now()>

In CFScript:

dateToday = "Today is: ";
dateToday = dateToday & now();
```

See how we added dateToday to another evaluation and replaced the dateToday variable name with the new contents? You can write it even shorter like this:

```cfml
<cfset dateToday = "Today is: ">
<cfset dateToday &= now()>

In CFScript:

dateToday = "Today is: ";
dateToday &= now();
```


## Outputting a Variable

You will often need to output the contents of the variables you create. One reason is to display the contents of a variable to the user, perhaps to display the username on a web page. Another reason is to verify the contents of a variable while you are in the process of writing or debugging your program.

### About cfoutput / writeOutput()

To display the contents of a variable to a user, use the cfoutput tag or the writeOutput CFScript function. The variable reference must be a simple value that can be displayed as text. This includes Strings, Numbers, Dates, Times, and so on. Complex variables, such as Structs, Arrays, Queries, Functions, and so on, can not be displayed with the cfoutput tag or the writeOutput function because they are not displayable as text.

#### Example

```cfml
<cfset dateToday = "Today is: #now()#">
<cfoutput>#DateToday#</cfoutput>

In CFScript:

dateToday = "Today is: #now()#";
writeOutput(dateToday);
```

The variable will be evaluated, and the variable contents will be added to the current end of the response buffer. i.e. it will be displayed to the user.

### About cfdump / writeDump()

To inspect or verify the contents of a variable while writing or debugging your program, use cfdump or the writeDump CFScript function. ColdFusion makes it very easy to see the contents of ANY variable by using the cfdump tag or the writeDump function. cfdump and writeDump() will convert the variable contents to a string representation and format it for easy viewing. This can be used to help debug your program.

#### Example

```cfml
<cfset dateToday = "Today is: #now()#">
<cfdump var = "#dateToday#">

<cfset dateArray = [dateFormat(now(), "short"), dateFormat(dateadd('d', 1, now()), "short"), dateFormat(dateadd('d', 2, now()), "short")]>
<cfdump var = "#dateArray#">

<cfset dateStruct = { today=dateFormat(now(), "short"), tomorrow=dateFormat(dateadd('d', 1, now()), "short"), later=dateFormat(dateadd('d', 2, now()), "short") }>
<cfdump var = "#dateStruct#">

In CFScript:

dateToday = "Today is: #now()#";
writeDump(dateToday);

dateArray = [dateFormat(now(), "short"), dateFormat(dateadd('d', 1, now()), "short"), dateFormat(dateadd('d', 2, now()), "short")];
writeDump(dateArray);

dateStruct = { today=dateFormat(now(), "short"), tomorrow=dateFormat(dateadd('d', 1, now()), "short"), later=dateFormat(dateadd('d', 2, now()), "short") };
writeDump(dateStruct);
```
